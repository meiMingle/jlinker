plugins {
    id 'java'
    id 'maven-publish'
}

group 'dev.xdark'
version '1.0.7'

repositories {
    mavenCentral()
}

sourceSets {
    java11 {
        java {
            srcDirs = ['src/main/java11']
        }
    }

}

dependencies {
    def junitVersion = '5.9.2'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"

    testImplementation 'org.ow2.asm:asm:9.5'
    testImplementation 'org.ow2.asm:asm-tree:9.5'

    compileOnly 'org.jetbrains:annotations:24.0.1'
    testCompileOnly 'org.jetbrains:annotations:23.0.0'
}

tasks.withType(JavaCompile) {
    options.compilerArgs.addAll(['-parameters', '-g:lines,source,vars'])
}

compileJava {
    options.release.set(8)
}

compileJava11Java {
    options.release.set(11)
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

jar {
    manifest {
        attributes "Multi-Release": 'true'
    }
    from sourceSets.java11.output
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java
    from sourceSets.java11.java
    classifier = 'sources'
}

publishing {
    repositories {
        maven {
            name = 'sources-repo'
            url = 'file:///tmp/sources-repo'
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}
